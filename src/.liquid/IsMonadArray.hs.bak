module IsMonadArray where

import           Liquid.ProofCombinators
import           Function
import           Relation
import           IsMonad
import           VUnit
import           VBool
import           VNat
import           VTuple
import           VList


-- Type. Index for monadic array.
{-@ type Index = VNat @-}
type Index = VNat


-- Data. Monadic Array
{-@
data IsMonadArray m a = IsMonadArray
  { isMonad :: IsMonad m
  , vread :: Index -> m a
  , vwrite :: Index -> a -> m ()
  , vread_vwrite :: i:Index ->
      {vbind isMonad (vread i) (vwrite i) = vlift isMonad vunit}
  , vwrite_vread :: i:Index -> x:a ->
    {vseq isMonad (vwrite i x) (vread i) = vseq isMonad (vwrite i x) (vlift isMonad x)}
  , vwrite_vwrite :: i:Index -> x:a -> x':a ->
      {vseq isMonad (vwrite i x) (vwrite i x') = vwrite i x'}
  , vread_vread :: i:Index -> f:(a -> a -> a) ->
      {vliftF2 isMonad f (vread i) (vread i) = vliftF isMonad (vdiagonalize f) (vread i)}
  , vread_commutative :: i:Index -> j:Index ->
      {IsCommutative (vseq isMonad) (vread i) (vread j)}
  , vwrite_commutative :: i:Index -> j:Index -> {i_neq_j : Proof | i /= j} -> x:a -> y:a ->
      {IsCommutative (vseq isMonad) (vwrite i x) (vwrite j y)}
  , vread_vwrite_commutative :: i:Index -> j:Index -> {i_neq_j : Proof | i /= j} -> x:a ->
      {IsCommutative (vseq isMonad) (vseq isMonad (vread i) (vlift isMonad vunit)) (vwrite j x)}
  }
@-}
data IsMonadArray m a = IsMonadArray
  { isMonad :: IsMonad m
  , vread  :: Index -> m a
  , vwrite :: Index -> a -> m ()
  , vread_vwrite :: Index -> Proof
  , vwrite_vread :: Index -> a -> Proof
  , vwrite_vwrite :: Index -> a -> a -> Proof
  , vread_vread :: Index -> (a -> a -> a) -> Proof
  , vread_commutative :: Index -> Index -> Proof -> Proof
  , vwrite_commutative :: Index -> Index -> Proof -> a -> a -> Proof
  , vread_vwrite_commutative :: Index -> Index -> Proof -> a -> Proof
  }


{-@ reflect vreadList @-}
vreadList :: IsMonadArray m a -> VNat -> VNat -> m (VList a)
vreadList isMonadArr i Zero = vlift_ Nil
 where
  vlift_   = vlift isMonad_
  isMonad_ = isMonad isMonadArr
vreadList isMonadArr i (Suc n) = vliftF2_ Cons
                                          (vread_ i)
                                          (vreadList_ (Suc i) n)
 where
  vread_     = vread isMonadArr
  vreadList_ = vreadList isMonadArr
  vliftF2_   = vliftF2 isMonad_
  isMonad_   = isMonad isMonadArr


{-@ reflect vwriteList @-}
vwriteList :: IsMonadArray m a -> VNat -> VList a -> m ()
vwriteList isMonadArr i Nil = vlift_ ()
 where
  vlift_   = vlift isMonad_
  isMonad_ = isMonad isMonadArr
vwriteList isMonadArr i (Cons x xs) = vseq_
  (vwrite_ i x)
  (vwriteList_ (Suc i) xs)
 where
  vseq_       = vseq isMonad_
  vwrite_     = vwrite isMonadArr
  vwriteList_ = vwriteList isMonadArr
  isMonad_    = isMonad isMonadArr


{-@ reflect vwriteListToLength @-}
vwriteListToLength :: IsMonadArray m a -> VNat -> VList a -> m VNat
vwriteListToLength isMonadArr i xs = vseq_ (vwriteList_ i xs)
                                           (vlift_ (vlength xs))
 where
  vwriteList_ = vwriteList isMonadArr
  vseq_       = vseq isMonad_
  vlift_      = vlift isMonad_
  isMonad_    = isMonad isMonadArr


{-@ reflect vwriteList2ToLength @-}
vwriteList2ToLength
  :: IsMonadArray m a
  -> VNat
  -> VTuple2D (VList a)
  -> m (VTuple2D VNat)
vwriteList2ToLength isMonadArr i (xs, ys) = vseq_
  (vwriteList_ i (vappend xs ys))
  (vlift_ (vlength xs, vlength ys))
 where
  vseq_       = vseq isMonad_
  vlift_      = vlift isMonad_
  vwriteList_ = vwriteList isMonadArr
  isMonad_    = isMonad isMonadArr


{-@ reflect vwriteList3ToLength @-}
vwriteList3ToLength
  :: IsMonadArray m a
  -> VNat
  -> VTuple3D (VList a)
  -> m (VTuple3D VNat)
vwriteList3ToLength isMonadArr i (xs, ys, zs) = vseq_
  (vwriteList_ i (vappend xs (vappend ys zs)))
  (vlift_ (vlength xs, vlength ys, vlength zs))
 where
  vseq_       = vseq isMonad_
  vlift_      = vlift isMonad_
  vwriteList_ = vwriteList isMonadArr
  isMonad_    = isMonad isMonadArr


-- {-@
-- vwriteList_vappend :: forall m a . isMonad:IsMonad m -> i:Index -> xs:VList a -> ys:VList a ->
--   {vwriteList i (vappend xs ys) = vseq isMonad (vwriteList i xs) (vwriteList (vadd i (vlength xs)) ys)}
-- @-}
-- vwriteList_vappend :: Index -> VList a -> VList a -> Proof
--
-- vwriteList_vappend isMonad i Nil ys =
--   vwriteList i (vappend Nil ys)
--     ==. vwriteList i ys
--     ==. vseq isMonad (vlift_ ()) (vwriteList i ys)
--     ?   vseq_identity (vwriteList i ys)
--     ==. vseq isMonad (vlift_ ()) (vwriteList (vadd i 0) ys)
--     ?   vadd_identity i
--     ==. vseq isMonad
--              (vwriteList i Nil)
--              (vwriteList (vadd i (vlength Nil)) ys)
--   where vlift_ = vlift isMonad
--
-- vwriteList_vappend isMonad i (Cons x xs) ys = _
